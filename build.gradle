plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.openjfx.javafxplugin' version '0.0.14'
    id 'com.palantir.git-version' version '3.0.0' // for gitVersion()
    id 'dev.hydraulic.conveyor' version '1.6'
    id 'org.beryx.jlink' version '2.25.0'
}
// Cheap enforcer
assert JavaVersion.current().equals(JavaVersion.VERSION_17)

group 'pro.javacard.nfc4pc'
version = file('version.txt').text.trim()

repositories {
    mavenLocal()
    maven {
        url 'https://javacard.pro/maven/'
    }
    maven {
        url 'https://javacard.pro/maven/SNAPSHOTS/'
    }
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    sourceCompatibility = '17'
    targetCompatibility = '17'
}

javafx {
    version = "20.0.1"
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.swing']
    //configuration = 'compileOnly'
}

application {
    mainModule = "pro.javacard.nfc4pc"
    mainClass = "pro.javacard.nfc4pc.MainWrapper"
}

jar {
    manifest {
        attributes 'Main-Class': application.mainClass
        attributes 'Implementation-Version': gitVersion()
    }
//        from {
//        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
//    }
//    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

shadowJar {
    archiveClassifier.set('')
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

    implementation 'com.github.martinpaljak:apdu4j-pcsc:23.03.22-SNAPSHOT'
    implementation 'com.dustinredmond.fxtrayicon:FXTrayIcon:4.0.1'
    implementation 'org.slf4j:slf4j-simple:2.0.7'
    implementation 'net.sf.jopt-simple:jopt-simple:5.0.4'
    implementation 'com.payneteasy:ber-tlv:1.0-11'
    implementation 'com.google.zxing:core:3.5.1'

    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:win"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:linux"
    runtimeOnly "org.openjfx:javafx-graphics:$javafx.version:mac-aarch64"

}

test {
    useJUnitPlatform()
}
